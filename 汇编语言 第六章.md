# 汇编语言 第六章 包含多个段的程序

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;前面的程序中，只有一个代码段。现在有一个问题是，如果程序想使用其它的空间存放数据，放在哪里呢？第5章中，我们提到过`0:200~0:2FF`是相对安全的，但是只有256字节，如果我们需要超过256字节的空间又该怎么办呢？

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;在操作系统的环境中，合法地通过操作系统取的空间都是安全的，因为操作系统不会让一个程序所用的空间和其它程序以及系统自己的空间相冲突。在操作系统允许的情况下，程序可以取得任意容量的空间。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;程序取得所需空间的方法有两种，一是在**加载程序的时候为程序分配**，二是**程序在执行的过程中像系统申请**，在我们的课程中，我们不讨论第二种方法。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;加载程序的时候为程序分配，其实我们已经体验到了这一过程，比如之前程序在加载之后，取得了代码段中代码的存储空间，即我们通过在源程序中定义段来进行内存空间的获取。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;上面是从内存的角度来谈及定义段的问题。我们再从程序规划的角度来谈一下定义段的问题。大多数的应用程序，都要处理数据，使用栈空间，当然也都必须有指令，为了程序设计上的清晰和方便，我们一般也都定义不同的段来存放它们。


## 6.1 代码段中使用数据

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;考虑这样一个问题，编程计算以下8个数据的和，结果存放在`ax`寄存器中。

`0123h、0456h、0789h、0abch、0defh、0fedh、0cbah、0987h`

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;我们可是使用如下程序:

```s
assume cs:code

code segment

    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h

    mov bx,0
    mov ax,0

s:  add ax,cs:[bx]
    add bx,2
    loop s

    mov ax,4c00h
    int 21h

code ends

end
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;上述程序中第一行中"dw"的含义是定义字型数据。即"define word"。程序编译连接后，我们加载进Debug，查看如下信息。

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.1.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.1.png" width="320" /></a></div>

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;当我们用`u`命令查看时，看到了一些让人读不懂的指令。为什么没有看到程序中的指令呢？实际上，我们在代码段的前16字节定义的是数据，从第16字节开始才是汇编指令，我们可以用`d`命令更加清楚查看程序中的前16字节。

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.2.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.2.png" width="320" /></a></div>

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;现在的问题是要如何执行上述程序呢，我们可以在Debug模式中将IP设置为10h，从而使CS:IP指向程序中的第一条指令。之后在继续执行。这样一来，我们只能使用Debug来执行程序，因为编译连接后的可执行文件由于程序的入口不是我们所希望执行的指令，因此直接运行会有问题。那到底如何才能直接执行呢？我们可以在源程序中指明程序的入口所在，代码如下:

```s
assume cs:code

code segment

    dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h

start:  mov bx,0  # 此处的start
        mov ax,0

s:      add ax,cs:[bx]
        add bx,2
        loop s

        mov ax,4c00h
        int 21h

code ends

end start  # 此处的start
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;这里我们要再次探讨`end`的作用。它除了通知编译器程序结束外，还可以通知编译器程序的入口在什么地方。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;现在的问题是，根据什么设置的CPU的CS:IP指向程序的第一条指令？这一点，**是由可执行文件中的描述信息指明的**。我们已经知道，可执行文件由描述信息和程序组成，程序源自源代码中的汇编指令和定义的数据；描述信息则主要是编译、连接程序对源程序中相关伪指令进行处理所得到的信息。我们在上面程序包含了`end start`这一伪指令，其描述了程序的结束和程序的入口。在编译、连接后，被转化为一个入口地址，存储在可执行文件的描述信息中。因此在最终的可执行文件中，这个入口地址的偏移地址部分为`10h`，最终加载程序的加载者通过程序的描述信息，设置CS:IP，这样CPU就行我们希望的地址开始执行。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;最终，我们的程序框架如下:

```s
assume cs:code

code segment
        .
        .
        数据
        .
        .
start:  
        .
        .
        代码
        .
        .
code ends

end start
```


## 6.2 在代码段中使用栈

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;本节中，我们完成如下任务，并在下节中学习新的方法来说明在代码段中使用栈的弊端。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;问题描述: 完成下面的程序，将程序中定义的数据逆序存放。

```s
assume cs:codesg

codesg segment

        dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
        # ?
codesg ends

end
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;为了完成上述程序，我们需要一段可以被当作栈的的内存空间，这段空间应该由系统来分配。因此我们可以在程序中通过定义一些数据来取得一段空间。程序如下:

```s
assume cd:codesg

codesg segment

        dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
        dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
        ; 我们定义了16个字型数据，这样程序加载后，我们取得了一段空间，可当栈来使用

start:  mov ax,cs
        mov ss,ax
        mov sp,30h ;(16+8)*2=48=30h

        mov bx,0
        mov cx,8

s:      push cs:[bx]
        add bx,2
        loop s

        mov bx,0
        mov cx,8

s0:     pop cs:[bx]
        add bx,2
        loop s0

        mov ax,4c00h
        int 21h

codesg ends

end start
```


## 6.3 将数据、代码、栈放入不同的段

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;上节中的例子有如下两个问题:

- 程序看着十分混乱
- 8086下，一个段最大就是64kb，因此数据、代码、栈中数据共同受限此大小。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;如何做呢？我们可以用定义代码段一样的方法来定义多个段，然后在这些段里面定义需要的数据，或通过数据来取得栈空间。具体的做法入下面程序所示，该程序完成上节中的相同任务。

```s
assume cs:code,ds:data,ss:stack

data segment
        dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
data ends

stack segment
        dw 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
stack ends

code segment

start:  mov ax,stack
        mov ss,ax
        mov sp,20h

        mov ax,data ;直接使用stack、data等段名表示段地址
        mov ds,ax
        mov bx,0

        mov cx,8

s:      push [bx]
        add bx,2
        loop s

        mov bx,0
        mov cx,8

s0:     pop [bx]
        add bx,2
        loop s0

        mov ax,4c00h
        int 21h

code ends

end start
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;上述代码中，CPU到底如何处理我们定义的段中的内容，是当作指令执行，当作数据访问，还是当作栈空间，完全是靠程序中具体的汇编指令，和汇编指令对CS:IP、SS:SP、DS等寄存器的设置决定的。跟`assume cs:code`等伪指令没有任何关系，我们还是只需要知道它将你定义的具有一定用途的段和相关的寄存器联系起来就可以了。


## 实验

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;本章的实验很重要，有些知识是从实验结果中分析得到，所以必须完成这个实验。

**1. 下面程序编译、连接在Debug中跟踪运行，回答问题**

```s
assume cs:code,ds:data,ss:stack

data segment
    dw 0123H,0456H,0798H,0abcH,0defH,0fedH,0cbaH,0987H
data ends

stack segment
    dw 0,0,0,0,0,0,0,0
stack ends

code segment

start:

    mov ax,stack
    mov ss,ax
    mov sp,16

    mov ax,data
    mov ds,ax

    push ds:[0]
    push ds:[2]
    pop ds:[2]
    pop ds:[0]

    mov ax,4c00H
    int 21H

code ends

end start
```

(1) 程序返回前，data段中的数据为多少？
(2) 程序返回前，cs=____ , ss=____ , ds=____ 。
(3) 程序加载后，code段的地址为X，则data段的地址为___、stack段的地址为____。

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.3.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.3.png" width="320" /></a></div>

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.4.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.4.png" width="320" /></a></div>

**答案:**

(1) data数据不变
(2) 076C、076B、076A
(3) X-2、X-1 (因为data和stack都是16字节的数据空间)

**2. 下面程序编译、连接在Debug中跟踪运行，回答问题**

```s
assume cs:code,ds:data,ss:stack

data segment
    dw 0123H,0456H
data ends

stack segment
    dw 0,0
stack ends

code segment

start:

    mov ax,stack
    mov ss,ax
    mov sp,16

    mov ax,data
    mov ds,ax

    push ds:[0]
    push ds:[2]
    pop ds:[2]
    pop ds:[0]

    mov ax,4c00H
    int 21H

code ends

end start
```

(1) 程序返回前，data段中的数据为多少？
(2) 程序返回前，cs=____ , ss=____ , ds=____ 。
(3) 程序加载后，code段的地址为X，则data段的地址为___、stack段的地址为____。
(4) 如果一个段占有N个字节，程序加载后，该段时间占有多少空间____。

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.5.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.5.png" width="320" /></a></div>

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.6.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.6.png" width="320" /></a></div>

**答案:**

(1) data数据不变
(2) 076C、076B、076A
(3) X-2、X-1 (不足16字节，按16字节计算)
(4) (N+15)/16 * 16

**3. 下面程序编译、连接在Debug中跟踪运行，回答问题**

```s
assume cs:code,ds:data,ss:stack

code segment
start:

    mov ax,stack
    mov ss,ax
    mov sp,16

    mov ax,data
    mov ds,ax

    push ds:[0]
    push ds:[2]
    pop ds:[2]
    pop ds:[0]

    mov ax,4c00H
    int 21H

code ends

data segment
    dw 0123H,0456H
data ends

stack segment
    dw 0,0
stack ends

end start
```

(1) 程序返回前，data段中的数据为多少？
(2) 程序返回前，cs=____ , ss=____ , ds=____ 。
(3) 程序加载后，code段的地址为X，则data段的地址为___、stack段的地址为____。

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.7.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.7.png" width="320" /></a></div>

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.8.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-6.8.png" width="320" /></a></div>

**答案:**

(1) data数据不变
(2) 076A、076D、076E
(3) X+3、X+4

**4. 如果将程序中的`end start`改为`end`，则上述哪个实验程序可以正常执行？为什么？**

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;第三个实验的程序可以正常执行，因为先定义的段的地址最小。
