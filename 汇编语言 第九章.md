# 汇编语言 第九章 转移指令的原理

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;**可以修改IP、或同时修改CS和IP的指令统称为转移指令**。概括的将，转移指令就是可以控制CPU执行内存中某处代码的指令。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;8086CPU的转移指令有以下几类。

- 只修改IP时，称为段内转移，比如: jmp ax
- 同时修改CS和IP时，称为段间转移，比如 jmp 1000:0

由于转移指令对IP修改范围的不同，段内转移又分为：短转移和近转移

- 短转移IP的修改范围是-128～127
- 近转移IP的修改范围是-32768~32767

8086CPU的转移指令分为以下几类:

- 无条件转移指令(如jmp)
- 条件转移指令
- 循环指令(如loop)
- 过程
- 中断

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;这些转移指令的转移的前提条件可能不同，但是转移的基本原理是相同，我们在这一章主要通过深入学习无条件转移指令jmp来理解CPU执行转移指令的基本原理。


## 9.1 操作符 offset

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;操作符`offset`在汇编语言中是由编译器处理的符号，它的功能是取得标号的偏移地址。比如下面的程序

```s
assume as:codesg

codesg segment

start:
    mov ax, offset start ; 相当于 mov ax,0
s:
    mov ax,offset s ; 相当于 mov ax,3

codesg ends
end start
```

## 9.2 jmp指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`jmp`为无条件转移指令，可以只修改IP，也可以同时修改CS和IP。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`jmp`指令要给出两种信息:

- (1) 转移的目的地址
- (2) 转移的距离(段间转移、段内短转移、段内近转移)

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;**不同给出目的地址的方法，和不同的转移位置**，对应有不同格式的`jmp`指令，下面的几节中，我们以给出目的地址的不同方法为主线，讲解jmp指令的主要应用格式和CPU执行转移指令的基本原理。


## 9.3 依据位置进行转移的jmp指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`jmp short 标号(转到标号处执行指令)`

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;这种格式的`jmp`是段内短转移。比如:

```s
assume cs:codesg

codesg segment

start:
    mov ax,0
    jmp short s
    add ax,1
s:
    inc ax
codesg ends

end start
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;上面程序执行完成后，ax中的值是1，因为越过了`add ax,1`这条指令。那么汇编语言`jmp short s`对应的机器指令应该是什么样的呢？我们先看以下别的汇编指令对应的机器指令

```s
汇编指令              机器指令
mov ax,0123H         B8 23 01
push ds:[0123H]      FF 36 23 01
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;可以看到，在一般的指令中，汇编指令中的idata(立即数)，不论它是表示一个数据还是内存单元的偏移地址，都会在对应的机器指令中出现，因为CPU执行的是机器指令，它必须要处理这些数据或地址。接下来，我们的DEBUG中将上述程序翻译成机器码，结果如下:

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.1.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.1.png" width="320" /></a></div>

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;我们可以清楚的看到，DEBUG将`jmp short s`中的`s`表示成了`inc ax`指令的偏移地址8，最终是`JUMP 0008`，这一切看似很合理，但是当我们查看对应的机器码时，发现为`EB 03`，注意，这个机器码竟然不包含转移的目的地址，那么CPU根据什么进行转移的呢？

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;CPU并不是神仙，它只能处理你提供给他的东西，因此`EB 03`中一定包含它转移的所有信息，现在的问题是，CPU如何知道将IP变为多少的呢？我们一步一步分析。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;从第二章中，我们了解了CPU执行指令的过程

- (1) 从CS:IP处取出指令，读取的指令进入指令缓冲器
- (2) (IP)=(IP)+所读取指令长度，从而指向下一条指令；
- (3) 指令指令。转到1，重复这个过程。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;按照这个过程，我们参照上图，当CPU执行`jump short s`的的时候：

- (1) 将`EB 03`读入指令缓冲器
- (2) (IP)=(IP)+2，为0005H，此时CS:IP指向 `add ax,1`
- (3) CPU执行指令缓冲器中的指令`EB 03`
- (4) 执行完成后，IP变为了0008H (从0005H)

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;这时，我们应该清楚的看出，虽然`EB 03`没有给出转移的目的地址，但是却告诉了CPU要转移的位移`03`，而这个位移，是编译器根据汇编指令中的“标号”计算出来的，具体的计算方法如下:

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.2.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.2.png" width="320" /></a></div>

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;总结一下，`jmp short 标号`的功能为`(IP)=(IP)+一个8位长度的位移`，这个位移等于`标号处的地址 - jmp指令后第一个字节的地址`，并且这个8位位移用补码表示，是编译器在编译时算出的。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;还有一种指令`jmp near ptr 标号`，和`jmp short 标号`类似，实现的是段内近转移，8位地址换成了16位地址。


## 9.4 转移的目的地址在指令中的jmp指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`jmp far ptr 标号`实现的是短间转移，又称为远转移，其功能是用标号处的地址和偏移地址修改CS和IP，例如下面得程序:

```s
assume cs:codesg

codesg segment

start:
    mov ax,0
    mov bx,0
    jmp far ptr s
    db 256 dup (0)

s:
    add ax,1
    inc ax

codesg ends

end start
```

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.3.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.3.png" width="320" /></a></div>

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;DEBUG中程序对应的机器码如下，注意其中的`jmp far ptr s`对应的机器码`EA 0B 01 BD 0B`中包含了转移的目的地址。


## 9.5 转移地址在寄存器中的jmp指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`jmp 16位reg`，功能就是`(IP)=(reg)`

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;这种指令，我们在2.11节中已经讲过了，这里不再详述。


## 9.6 转移地址在内存中的jmp指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;转移地址在内存中的jmp指令有两种格式:

- (1) jmp word ptr 内存单元地址(段内转移)

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;功能: 从内存单元处存放这一个字，这个字是转移的目的偏移地址，例如

```s
mov ax,0123H
mov ds:[0],ax
jmp word ptr ds:[0]

; (IP) = 0123H
```

- (2) jmp dword ptr内存单元地址(段间转移)

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;功能：从内存单元开始处的存放这两个字，高出字是转移的段地址，低地址处是转移的目的偏移地址

```s
mov ax,0123H
mov ds:[0],ax
mov word ptr ds:[2],0
jmp dword ptr ds:[0]

; (CS)=0, (IP)=0123H
```


## 9.7 jcxz指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`jcxz`指令为有条件转移指令，**所有的有条件转移指令都是短转移**，在对应的机器码中包含了转移的位移，而不是目的地址。对IP的修改范围都为-128～127。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;指令格式: `jcxz 标号` (如果(cx)=0,转移到标号处执行)

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;操作: 当(cx)=0时，`(IP)=(IP)+一个8位长度的位移`，位移等于标号处的地址 - jcxz指令后第一个字节的地址。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;如果(cx)!=0，则程序向下执行。


## 9.8 loop指令

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;`loop`指令为循环指令，**所有的循环指令都是短转移**，在对应的机器码中包含了转移的位移，而不是目的地址。对IP的修改范围都为-128～127。

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;指令格式`loop 标号`

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;操作，(cx)=(cx)-1，如果(cx)!=0，则`(IP)=(IP)+一个8位长度的位移`，位移等于标号处的地址 - loop指令后第一个字节的地址。


## 9.9 根据位移进行转移的意义

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;这一章节中，我们介绍了一些转移指令，如`jcxz 标号`等，它们对IP的修改是根据目的地址和起始地址之间的位移进行的，在它们的机器码中不包含转移的目的地址，而包含的是到目的地址的位移，那究竟为什么如此设计呢？例如如下程序:

```s
汇编指令         机器指令
mov cx,6        B9 06 00
mov ax,10h      B8 10 00
s: add ax,ax    01 c0
loop s          E2 FC ;(FC是-4的补码)
```

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;之所以有这种位移的设计，是为了保证上述代码在内存中的不同位置都可以正确执行，因为`loop s`在执行时涉及到的只是相对于`s`的位移，而非`s`的地址。否则，如果记录的是`s`的地址，那就对程序段在内存中的偏移地有了强严格的要求，否则程序出错，因为地址处不是`s`对应的指令。所以，这种位移的方式可不关系`s`处指令的实际地址是多少，因为转移位移是不变的。


## 9.10 编译器对转移位移超界的检测

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;如果源程序中出现了转移范围超界的问题，在编译的时候，编译器将报错。


## 实验

**1. 分析一个奇怪的程序，思考这个程序可以正常返回么？为什么是这个结果?**

```s
assume cs:codesg

codesg segment
    mov ax,4c00H ; 最终程序走到了这里，正常返回。
    int 21H

start:
    mov ax,0
s:
    nop ;空占一个字节而已 当jmp过来的时候，此处的nop为 EB F6，正好对应 jmp 0000H
    nop

    mov di,offset s  ; mov di,0008
    mov si,offset s2  ; mov si,0020
    mov ax,cs:[si] ; cs:[0020]中的数据是 `EB F6`，是一个jmp指令，往前相对位移是F6长度
    mov cs:[di],ax ; 把这个EB F6 mov 到了 s处的，替换了两个nop

s0:
    jmp short s ; 顺序执行的这里，跳到s处

s1:
    mov ax,0
    int 21h
    mov ax,0

s2:
    jmp short s1
    nop

codesg ends
end start
```

**分析**

<div class="separator" style="clear: both;"><a href="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.4.png" style="display: block; padding: 1em 0px; text-align: center;"><img alt="" border="0" data-original-height="1202" data-original-width="1726" src="https://raw.githubusercontent.com/lidongze0629/blog/main/images/assembly-language-9.4.png" width="320" /></a></div>

&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;上述是程序加载后，的指令代码。分析过程在上述代码的注释中。


**2. 屏幕中间，分别显示绿色、绿底红色、白底蓝色的字符串'welcome to masm!'**

```s
assume cs:codesg

data segment
    db 'welcome to masm!'
    db 02H, 24H, 71H ;绿色、绿底红色、白底蓝色对应的数值
data ends

codesg segment
    start:
        mov ax,data
        mov ds,ax
        mov bx,0
        mov di,0a40H  ; 这一行的中间位置

        mov ax,0B800H ;B8000H~BFFFFH 为80 * 25彩色字符模式的显示缓冲区
        mov es,ax

        mov cx,16

    s:  mov al,ds:[bx]
        mov es:[di],al  ; 一个字节是数值
        mov ah,02H
        mov es:[di+1],ah  ; 紧接着下面一个字节数属性，包括闪烁、RGB、前景RGB、高亮等

        mov si,di
        add si,0a0H ; 下一行
        mov es:[si],al
        mov ah,24H
        mov es:[si+1],ah

        add si,0a0H
        mov es:[si],al
        mov ah,71H
        mov es:[si+1],ah

        inc bx
        inc di
        inc di

        loop s

        mov ax,4c00h
        int 21h

codesg ends
end start
```